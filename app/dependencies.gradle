def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

def test = dependencyGroup {
    testImplementation 'junit:junit:4.12'
    testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
}

def androidTest = dependencyGroup {
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

def ui = dependencyGroup {
    def support_version = '26.1.0'
    implementation 'com.android.support:appcompat-v7:' + support_version
    implementation 'com.android.support:cardview-v7:' + support_version
    implementation 'com.android.support:recyclerview-v7:' + support_version
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
}

def kotlin = dependencyGroup {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}
def network = dependencyGroup {
    def retrofit_version = '2.3.0'
    implementation 'com.squareup.retrofit2:retrofit:' + retrofit_version
    implementation 'com.squareup.retrofit2:converter-gson:' + retrofit_version
    implementation 'com.squareup.retrofit2:adapter-rxjava2:' + retrofit_version
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.github.bumptech.glide:glide:4.3.1'
    kapt 'com.github.bumptech.glide:compiler:4.3.1'
}
def rx = dependencyGroup {
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
}

def dependencyInjection = dependencyGroup {
    def dagger_version = '2.14'
    implementation 'com.google.dagger:dagger-android:' + dagger_version
    implementation 'com.google.dagger:dagger-android-support:' + dagger_version
    kapt 'com.google.dagger:dagger-android-processor:' + dagger_version
    kapt 'com.google.dagger:dagger-compiler:' + dagger_version
}

def archComponent = dependencyGroup {
    def arch_component_version = '1.1.0'
    implementation 'android.arch.lifecycle:viewmodel:' + arch_component_version
    kapt 'android.arch.lifecycle:compiler:' + arch_component_version
    implementation 'android.arch.lifecycle:extensions:' + arch_component_version
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    kapt "com.android.databinding:compiler:$gradle_version"
    test()
    androidTest()
    ui()
    kotlin()
    network()
    rx()
    dependencyInjection()
    archComponent()
}